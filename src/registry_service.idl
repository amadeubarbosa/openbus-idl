#ifndef __TECGRAF_OPENBUS_CORE_REGISTRY_SERVICE_IDL__
#define __TECGRAF_OPENBUS_CORE_REGISTRY_SERVICE_IDL__

#include "core.idl"
#include "scs.idl"
#include "access_control_service.idl"

module tecgraf {

module openbus {

module core {

module v1_06 {

/**
 * \brief Módulo do Serviço de Registro.
 */
module registry_service {

typedef sequence<string> PropertyValue;

/**
 * \brief Representa uma propriedade.
 */
struct Property {
  string fName;
  PropertyValue fValue;
};
typedef sequence<Property> PropertyList;

/**
 * \brief Representa uma oferta de serviço.
 */
struct ServiceOffer {
  PropertyList fProperties; /**< \brief Propriedades. */
  scs::core::IComponent fMember; /**< \brief O membro que está ofertando o serviço. */
};
typedef sequence<ServiceOffer> ServiceOfferList;

/** \brief O identificador do registro de um serviço. */
typedef Identifier OfferIdentifier;

/**
 * \brief Representa uma faceta.
 */
typedef string Facet;
typedef sequence<Facet> FacetList;

/**
 * \brief Representa uma entrada completa de oferta de serviço.
 */
struct ServiceOfferEntry {
  ServiceOffer aServiceOffer; /**< \brief Oferta de servico. */
  tecgraf::openbus::core::v1_06::access_control_service::Credential
      aCredential;/**< \brief Credential do servico que disponibiliza a oferta. */
  OfferIdentifier identifier; /**< \brief O identificador do registro de um serviço. */
  string authorizedFacets;/**< \brief As facetas (serializadas em strings) autorizadas do membro que oferta o servico */
  PropertyList properties;/**< \brief As propriedades da oferta */
};

/**
 * \brief Representa uma lista de entradas completas de ofertas de serviço.
 */
typedef sequence<ServiceOfferEntry> ServiceOfferEntryList;

/** \brief Indica que o serviço tentou registrar alguma faceta não autorizada */
exception UnauthorizedFacets {
  FacetList fFacets; /**< \brief Lista de facetas não autorizadas */
};

/** \brief Indica que Serviço de Registro não possui a oferta de serviço indicada */
exception ServiceOfferDoesNotExist {};

/** \brief Indica que o membro é inválido */
exception InvalidMember {};

/** \brief Indica que as propriedades são inválidas */
exception InvalidProperties{};

/**
 * \brief Representa o serviço de registro.
 */
interface IRegistryService {
  /**
   * \brief Registra uma oferta de serviço.
   *
   * \param fProperties Lista de propriedades.
   * \param fMember O componente a ser registrado.
   *
   * \return Um identificador serviço ofertado.
   *
   * \exception InvalidMember Componente inválido.
   * \exception InvalidProperties Propriedades inválidas.
   * \exception UnauthorizedFacets Serviço sem autorização para publicar uma
   *  ou mais facetas.
   * \exception ServiceFailure Erro na execução do serviço
   */
  OfferIdentifier register(in PropertyList fProperties,
      in scs::core::IComponent fMember)
      raises (InvalidMember, InvalidProperties, UnauthorizedFacets,
      ServiceFailure);

  /**
   * \brief Remove uma oferta de serviço.
   *
   * \param fIdentifier O identificador do registro da oferta do serviço.
   *
   * \exception ServiceOfferDoesNotExist Identificador de oferta inexistente.
   * \exception ServiceFailure Erro na execução do serviço
   */
  void unregister(in OfferIdentifier fIdentifier)
      raises (ServiceOfferDoesNotExist, ServiceFailure);

  /**
   * \brief Atualiza uma oferta de serviço.
   *
   * \param fIdentifier O identificador do registro da oferta do serviço.
   * \param fProperties O novo conjunto de propriedades associado à oferta
   *
   * \exception InvalidProperties Propriedades inválidas
   * \exception ServiceOfferDoesNotExist O membro não possui nenhuma oferta
   *   relacionada com o identificador informado.
   * \exception ServiceFailure Erro na execução do serviço
   */
  void setOfferProperties (in OfferIdentifier fIdentifier,
      in PropertyList fProperties)
      raises (ServiceOfferDoesNotExist, InvalidProperties, ServiceFailure);

  /**
   * \brief Realiza uma busca por ofertas através de uma lista de facetas.
   *
   *   Serão selecionadas as ofertas de serviços que implementam todas as 
   *   facetas descritas em facets.
   *
   * \param fFacets As facetas da busca.
   *
   * \return As ofertas encontradas.
   *
   * \exception ServiceFailure Erro na execução do serviço
   */
  ServiceOfferList find (in FacetList fFacets) raises (ServiceFailure);

  /**
   * \brief Realiza uma busca por ofertas através de uma lista de facetas e critérios.
   * 
   *   Serão selecionadas as ofertas de serviços que implementam todas as 
   *   facetas descritas em facets, e, que satisfaçam aos critérios 
   *   especificados.
   *
   * \param facets As facetas da busca.
   * \param criteria Os critérios da busca.
   *
   * \return As ofertas encontradas.
   *
   * \exception ServiceFailure Erro na execução do serviço
   */
  ServiceOfferList findByCriteria (in FacetList fFacets,
      in PropertyList fCriteria) raises (ServiceFailure);
};

interface ServiceOfferEntryManagement {
  /**
   * \brief Realiza uma busca por ofertas através de uma lista de facetas e 
   *   critérios, se forem especificados.
   * 
   *   Serão selecionadas as ofertas de serviços que implementam todas as 
   *   facetas descritas em facets, e, que satisfaçam aos critérios 
   *   (propriedades) especificados.
   *
   * \param facets As facetas da busca.
   * \param criteria Os critérios da busca.
   *
   * \return As entradas das ofertas encontradas.
   */
  ServiceOfferEntryList localFind (in FacetList facets, in PropertyList criteria);
};

/*----------------------------- Interface ------------------------------*/

/** \brief Identificador de interface. */
typedef string InterfaceIdentifier;

/** \brief Seqüência de indetificadores de interface. */
typedef sequence<InterfaceIdentifier> InterfaceIdentifierList;

exception InvalidInterfaceIdentifier {};

/** \brief Identificador de interface em uso por alguma autorização. */
exception InterfaceInUse {};

/** \brief Identificador de interface não encontrado. */
exception InterfaceIdentifierNotFound {};

/** \brief Identificador de interface não existente. */
exception InterfaceDoesNotExist {};

/** \brief Identificador de interface já cadastrado.*/
exception InterfaceAlreadyExists {};

/*---------------------------- Autorização -----------------------------*/

/** Tipo da autorização */
enum AuthorizationType { ATUser, ATSystemDeployment };

struct Authorization {
  string fEntityId;
  string fInterfaceIdentifier;
};
typedef sequence<Authorization> AuthorizationList;

/** \brief Autorização para uma implantação de sistema. */
struct EntityAuthorizations {
  string fEntityId; /**< Identificador do membro */
  AuthorizationType fType; /**< Tipo de membro que tem autorização */
  InterfaceIdentifierList fAuthorized; /**< Interfaces autorizadas */
};

/** \brief Seqüência de autorizações. */
typedef sequence<EntityAuthorizations> EntityAuthorizationsList;

/**
 * \brief Representa as facetas em uma entrada no Serviço de Registro
 */
struct OfferedInterfaces {
  OfferIdentifier fOfferId; /**< Identificar da oferta */
  string fEntityId; /**< Identificador do membro que efetuou o registro */
  InterfaceIdentifierList fInterfaces; /**< Interfaces não autorizadas */
};

typedef sequence<OfferedInterfaces> OfferedInterfacesList;

/** \brief Membro não existe. */
exception EntityDoesNotExist {};

/** \brief Autorização não existe. */
exception AuthorizationDoesNotExist {};

/** \brief Expressão regular inválida. */
exception InvalidRegularExpression {};

/**
 * \brief Interface de gerenciamento de autorizações de serviços.
 *
 */
interface IManagement {

  /*--------------------------- Interface ------------------------------*/

  /**
   * \brief Cadastra um identificador de interface aceito
   *   pelo Serviço de Registro.
   *
   * \param fInterfaceId Identificador de interface.
   *
   * \exception InterfaceAlreadyExists Identificador já cadastrado.
   * \exception InvalidInterfaceIdentifier Identificador inválido.
   * \exception ServiceFailure Erro na execução do serviço
   */
  void addInterface(in InterfaceIdentifier fInterfaceId)
      raises (InterfaceAlreadyExists, InvalidInterfaceIdentifier,
      ServiceFailure);

  /**
   * \brief Remove o identificador.
   *
   * \param fInterfaceId Identificador de interface.
   *
   * \exception InterfaceDoesNotExist Identificador não cadastrado.
   * \exception InterfaceInUse Interface é referenciada por algum elemento do 
   *   sistema.
   * \exception ServiceFailure Erro na execução do serviço
   */
  void removeInterface(in InterfaceIdentifier fInterfaceId)
      raises (InterfaceInUse, InterfaceDoesNotExist, ServiceFailure);

  /**
   * \brief Recupera todos os identificadores de interface cadastrados.
   *
   * \return Seqüência de identificadores de interface.
   *
   * \exception ServiceFailure Erro na execução do serviço
   */
  InterfaceIdentifierList getInterfaces() raises (ServiceFailure);

  /*-------------------------- Autorização -----------------------------*/

  /**
   * \brief Autoriza uma entidade a exportar a interface.
   *
   * \param fEntityId Identificador da entidade cadastrada.
   * \param fInterfaceId Identificador da interface cadastrada.
   *
   * \exception EntityDoesNotExist Identificador da entidade não está cadastrada.
   * \exception InterfaceDoesNotExist Identificador da interface não está cadastrada.
   * \exception InvalidRegularExpression Identificador da interface possui
   *   uma expressão regular inválida.
   * \exception ServiceFailure Erro na execução do serviço
   */
  void grant(in string fEntityId, in InterfaceIdentifier fInterfaceId)
      raises (EntityDoesNotExist, InterfaceDoesNotExist,
      InvalidRegularExpression, ServiceFailure);

  /**
   * \brief Revoga a autorização para exportar a interface.
   *
   * \param fEntityId Identificador da entidade cadastrada.
   * \param fInterfaceId Identificador da interface cadastrada.
   *
   * \return TRUE caso uma autorização tenha sido revogada, ou FALSE se a
   *         autorização especificada não existia.
   *
   * \exception EntityDoesNotExist Identificador da entidade não está cadastrada.
   * \exception InterfaceDoesNotExist Identificador da interface não está cadastrada.
   * \exception ServiceFailure Erro na execução do serviço
   */
  boolean revoke(in string fEntityId, in InterfaceIdentifier fInterfaceId)
      raises (EntityDoesNotExist, InterfaceDoesNotExist, ServiceFailure);

  /**
   * \brief Remove a autorização da entidade.
   *
   * \param fEntityId Identificador da entidade.
   *
   * \return TRUE caso alguma autorização existente dessa entidade tenha sido
   *         revogada, ou FALSE se a entidade não possuia nenhuma autorização.
   *
   * \exception EntityDoesNotExist Identificador da entidade não está cadastrada.
   * \exception ServiceFailure Erro na execução do serviço
   */
  boolean revokeAll(in string fEntityId)
      raises (EntityDoesNotExist, ServiceFailure);

  /**
   * \brief Recupera as autorizações de uma entidade.
   *
   * \param fEntityId Identificador da entidade cadastrada.
   *
   * \return Descrição das autorizações da entidade.
   *
   * \exception EntityDoesNotExist Identificador da entidade não está cadastrada.
   * \exception ServiceFailure Erro na execução do serviço
   */
  EntityAuthorizations getEntityAuthorizations(in string fEntityId)
      raises (EntityDoesNotExist, ServiceFailure);

  /**
   * \brief Recupera todas as autorizações cadastradas.
   *
   * \return Seqüência de autorizações.
   *
   * \exception ServiceFailure Erro na execução do serviço
   */
  EntityAuthorizationsList getAllEntityAuthorizations() raises (ServiceFailure);

  /**
   * \brief Recupera as autorizações que contêm \e todas as interfaces
   *  fornecidas em seu conjunto de interfaces autorizadas.
   *
   * \param fInterfaceIds Identificador das interfaces.
   *
   * \return Seqüência de autorizações.
   *
   * \exception InterfaceDoesNotExist Identificador da interface não está cadastrada.
   * \exception ServiceFailure Erro na execução do serviço
   */
  EntityAuthorizationsList getAuthorizationsByInterfaceId(
      in InterfaceIdentifierList fInterfaceIds)
      raises (InterfaceDoesNotExist, ServiceFailure);

  /**
   * \brief Recupera todas as interfaces oferecidas no registro.
   *
   * \return Seqüência de interfaces registradas.
   *
   * \exception ServiceFailure Erro na execução do serviço
   */
   OfferedInterfacesList getOfferedInterfaces() raises (ServiceFailure);

  /**
   * \brief Recupera todas as interfaces oferecidas no registro por 
   * uma dada entidade.
   *
   * \param fEntityId Identificador da entidade.
   *
   * \return Seqüência de interfaces registradas pela entidade.
   *
   * \exception EntityDoesNotExist Identificador da entidade não está cadastrada.
   * \exception ServiceFailure Erro na execução do serviço
   */
  OfferedInterfacesList getOfferedInterfacesByEntity(in string fEntityId)
      raises (EntityDoesNotExist, ServiceFailure);
};

}; // registry_service

}; // version

}; // core

}; // openbus

}; // tecgraf

#endif
