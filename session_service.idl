#ifndef SS_IDL
#define SS_IDL

#include "core.idl"

module OpenBus {

/**
 * \brief Módulo do Serviço de Sessão.
 */
module SS {

/** \brief Representa o identificador de uma sessão. */
typedef Identifier SessionIdentifier;
/** \brief Representa o identificador do membro de uma sessão. */
typedef Identifier MemberIdentifier;

/**
 * \brief Representa uma sessão.
 */
interface ISession {
    /**
     * \brief Obtém o identificador da sessão.
     *
     * \return O identificador da sessão.
     */
    SessionIdentifier getIdentifier();
    /**
     * \brief Adiciona um membro à sessão.
     *
     * \param member O membro.
     *
     * \return O identificador do membro.
     */
    MemberIdentifier addMember(in IMember member);
    /**
     * \brief Remove um membro da sessão.
     *
     * \param memberIdentifier O identificador do membro.
     *
     * \return \c true caso o membro tenha sido removido, ou \c false, caso contrário.
     */
    boolean removeMember(in MemberIdentifier memberIdentifier);
    /**
     * \brief Obtém todos os membros da sessão.
     *
     * \return A sequência de membros. Pode estar vazia caso não existam membros participando da
     * sessão.
     */
    MemberList getMembers();
};
/** \brief Representa uma sequência de sessões. */
typedef sequence<ISession> SessionList;

/**
 * \brief Representa o serviço de sessão.
 */
interface ISessionService {
    /**
     * \brief Cria uma sessão associada a uma credencial.
     *
     * \param[out] session A sessão criada.
     *
     * \return \c true, caso a sessão tenha sido criada, ou \c false, caso contrário.
     */
    boolean createSession(out ISession session);
    /**
     * \brief Obtém a sessão associada a uma credencial.
     *
     * \return A sessão ou \c nil, caso a credencial esteja inválida ou não exista sessão
     * associada à credencial.
     */
    ISession getSession();
};

/**
 * \brief Membro que representa o serviço de sessão.
 *
 * Facetas:
 * - SessionService
 */
interface ISessionServiceComponent : IMember {
};

};

};

#endif
